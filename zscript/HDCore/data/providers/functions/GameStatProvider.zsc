class HDCoreFunctionProvider_GameStat : HDCoreFunctionProvider {

    private HDCoreGameStatsHandler _handler;

    override void setValue(string newVal) {
        HDCore.log('HDCoreLib.GameStatProvider', LOGGING_TRACE, "Setting value from ".._value.." to "..newVal);

        super.setValue(newVal);
        
        getHandler();
    }

    override string getStringValue() {
        getHandler();

        let v = !!_handler ? _handler.getValueOrDefault(getStringParam("key"), getStringParam("defaultValue")) : "";

        HDCore.log("HDCoreLib."..GetClassName(), LOGGING_TRACE, "getStringValue() => "..v);

        return v;
    }

    override name getNameValue() {
        name value = getStringValue();
        return value;
    }

    override int getIntValue() {
        return getStringValue().toInt(10);
    }

    override double getDoubleValue() {
        return getStringValue().toDouble();
    }

    override bool getBoolValue() {
        return getStringValue() == 'true';
    }

    private void getHandler() {
        if (!_handler) _handler = HDCoreGameStatsHandler(EventHandler.find("HDCoreGameStatsHandler"));
    }
}