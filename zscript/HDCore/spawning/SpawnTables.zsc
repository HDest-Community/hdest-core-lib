class HDCoreSpawnTable play {

    // Unique Name of the table.
    name tableName;

    // Name of thing to react to being spawned.
    name spawnName;

    // List of entries of things to spawn.
    Array<HDCoreSpawnTableEntry> _entries;

    // Whether or not to replace the original thing or just spawn alongside it.
    bool replace;

    /**
     * Creates a spawn table for a given thing by name, as well as an optional unique table name,
     * whether the entries are considered after initial map spawn, and whether the table replaces the original thing.
     * @param {name} spawnName The className of the thing that might spawn.
     * @param {name} [tableName] An optional name to give the table, defaults to spawnName.
     * @param {bool} [replace=true] If true, the selected entry in the table will replace the original actor.
     * @returns {HDCoreSpawnTable} The newly created table.
     */
    static HDCoreSpawnTable create(name spawnName, name tableName = '', bool replace = true) {
        HDCoreSpawnTable table = HDCoreSpawnTable(new('HDCoreSpawnTable'));

        table.tableName = tableName != '' ? tableName : spawnName;
        table.spawnName = spawnName;
        table.replace = replace;

        table.clearEntries();

        return table;
    }

    bool equals(HDCoreSpawnTable other) {
        return (tableName == other.tableName || spawnName == other.spawnName) && replace == other.replace;
    }

    string toString() {

        let entriesStr = "[";

        forEach (entry : _entries) entriesStr = entriesStr..", "..entry.toString();

        entriesStr = entriesStr.."]";

        return String.format(
            "{ \"tableName\": \"%s\", \"spawnName\": \"%s\", \"replace\": %i, \"totalWeight\": %.3f, \"entries\": %s }",
                 tableName,             spawnName,             replace,      getTotalWeight(_entries), entriesStr
        );
    }

    void addEntry(HDCoreSpawnTableEntry entry) {
        HDCore.Log('HDCoreLib', LOGGING_TRACE, "Adding '"..entry.name.."' to Spawn Table '"..tableName.."' Entries...");

        _entries.push(entry);
    }

    void addEntries(Array<HDCoreSpawnTableEntry> arr) {
        HDCore.Log('HDCoreLib', LOGGING_TRACE, "Appending entries to Spawn Table '"..tableName.."'...");

        _entries.append(arr);
    }

    void removeEntry(name name) {
        HDCore.Log('HDCoreLib', LOGGING_TRACE, "Removing '"..name.."' from Spawn Table '"..tableName.."' Entries...");

        HDCoreSpawnTableEntry tableEntry;

        forEach (entry : _entries) {
            if (entry.name == name) {
                HDCore.Log('HDCoreLib', LOGGING_TRACE, "Entry for '"..entry.name.."' found.");

                tableEntry = entry;
                break;
            }
        }

        _entries.delete(_entries.find(tableEntry));
    }

    void clearEntries() {
        HDCore.Log('HDCoreLib', LOGGING_TRACE, "Clearing all entries from Spawn Table '"..tableName.."'.");

        _entries.clear();
    }

    HDCoreSpawnTableEntry getEntry(name name, bool persistentOnly = false) {
        forEach (entry : _entries) if (entry.name == name && (!persistentOnly || entry.isPersistent)) return entry;

        return null;
    }

    void getEntries(out Array<HDCoreSpawnTableEntry> arr, bool persistentOnly = false) {
        arr.clear();

        forEach (entry : _entries) {

            // If entry is not nested, yet has an invalid actor, skip.
            if (entry is 'HDCoreSpawnTableSingleEntry' && !HDCore.CheckClassExists(entry.name)) {
                HDCore.Log('HDCoreLib', LOGGING_WARN, "Spawn Table Entry for "..tableName.." includes a class '"..entry.name.."' that does not exist!");

                continue;
            }

            // If we're beyond inital map spawns and are not persistent, skip.
            if (persistentOnly && !entry.isPersistent) {
                HDCore.Log('HDCoreLib', LOGGING_DEBUG, "Spawn Table Entry '"..entry.name.."' is not persistent, skipping.");

                continue;
            }


            // If weight is negative, treat as only possible outcome (replace all)
            if (entry.getWeight(persistentOnly) < 0.0) {
                HDCore.Log('HDCoreLib', LOGGING_DEBUG, "'"..entry.name.."' has negative weight, returning single entry.");

                arr.clear();
                arr.push(entry);
                return;
            }

            // Otherwise, as long as we have a valid weight, add it to array
            if (!HDGMGlobalMaths.nearZero(entry.getWeight(persistentOnly))) {
                HDCore.Log('HDCoreLib', LOGGING_TRACE, "Adding '"..entry.name.."' to list of relevant entries.");

                arr.push(entry);
            }
        }

        if (!arr.size()) HDCore.Log('HDCoreLib', LOGGING_WARN, "No valid entries found for Spawn Table \""..tableName.."\". This may cause unexpected behavior!");
    }

    HDCoreSpawnTableEntry getRandomEntry(bool persistentOnly = false) {
        HDCore.Log('HDCoreLib', LOGGING_TRACE, "Getting random entry from Spawn Table '"..tableName.."'...");

        Array<HDCoreSpawnTableEntry> entries;
        entries.clear();

        getEntries(entries, persistentOnly);

        Array<double> weights;
        weights.clear();

        getWeights(weights, entries, persistentOnly);

        if (HDCore.ShouldLog('HDCoreLib', LOGGING_DEBUG)) {
            let msg = tableName.." Entries:\n";
            let totalWeight = getTotalWeight(entries, persistentOnly);
            
            forEach (entry : entries) {
                let weight = entry.getWeight(persistentOnly);

                msg = msg.." * "..entry.name.." ["..weight.." ("..((weight / totalWeight) * 100.0).."%)]\n";
            }

            HDCore.Log('HDCoreLib', LOGGING_DEBUG, msg);
        }

        HDCoreSpawnTableEntry entry;

        if (entries.size() > 1) {
            entry = entries[HDCore.getWeightedRandom(weights)];
        } else if (entries.size() > 0) {
            entry = entries[0];
        }
        
        HDCore.Log('HDCoreLib', LOGGING_DEBUG, "Selected Entry: "..(entry ? "\""..entry.name.."\"" : "Nothing"));

        return entry;
    }

    static void getWeights(out Array<double> weights, Array<HDCoreSpawnTableEntry> entries, bool persistentOnly = false) {
        weights.clear();

        forEach (entry : entries) weights.push(entry.getWeight(persistentOnly));
    }

    static double getTotalWeight(Array<HDCoreSpawnTableEntry> entries, bool persistentOnly = false) {
        let weight = 0.0;

        forEach (entry : entries) {
            let w = entry.getWeight(persistentOnly);
            if (w > 0.0) {
                weight += w;
            } else {
                return 1.0;
            }
        }

        return weight;
    }
}

class HDCoreSpawnTableEntry play abstract {

    // Name of entry.
    // Single Entries are named according to the thing they'll spawn,
    // whereas Nested Entries are named according to their table's name.
    name name;

    // The relative weight that this entry will spawn within the table
    double weight;

    // Whether or not to use this table beyond initial map spawn.
    bool isPersistent;

    // Chance out of 256 that the thing being spawned will succeed, otherwise nothing will spawn in its place.
    int chance;

    // Whether or not this entry should be considered final.
    bool isFinal;

    // Getter to return the name of the thing to spawn for this entry
    abstract name getValue(bool persistentOnly = false);

    // Getter to return the weight of this entry
    abstract double getWeight(bool persistentOnly = false);

    // Getter to return the relevant list of entries for this 
    abstract void getEntries(out Array<HDCoreSpawnTableEntry> entries, bool persistentOnly = false);

    // Helper method to easily write this entry to the console
    abstract string toString();
}

class HDCoreSpawnTableSingleEntry : HDCoreSpawnTableEntry {

    /**
     * Creates an entry for a given thing by name and its chances of spawning within the table.
     * @param {name} name The className of the thing that might spawn.
     * @param {double} [weight=1.0] The relative weight/bias of the thing in the table.
     * @param {bool} [persists=false] If true, the newly created entry will be marked as persistent.
     * @param {int}  [chance=256] chance out of 256 that the thing being spawned will actually spawn.
     * @param {bool} [isFinal=false] If true, the selected entry will be considered final.
     * @returns {HDCoreSpawnTableEntry} The newly created table entry.
     */
    static HDCoreSpawnTableEntry create(name name, double weight = 1.0, bool persists = false, int chance = 256, bool isFinal = false) {
        HDCoreSpawnTableSingleEntry entry = HDCoreSpawnTableSingleEntry(new('HDCoreSpawnTableSingleEntry'));

        entry.name         = name;
        entry.weight       = weight;
        entry.isPersistent = persists;
        entry.chance       = chance;
        entry.isFinal      = isFinal;

        return entry;
    }

    override name getValue() {

        // If a random value (0-255) falls below the success chance, return what's in the entry.
        // Otherwise return a "nothing" dummy spawner to simulate a RandomSpawner picking and failing to drop a thing.
        return (chance > 255 || random() < chance) ? name : 'NullSpawner';
    }

    override double getWeight() {
        return weight;
    }

    override void getEntries(out Array<HDCoreSpawnTableEntry> entries) {
        // no-op
    }

    override string toString() {
        return String.format(
            "{ \"name\": \"%s\", \"weight\": %.3f, \"isPersistent\": %i, \"chance\": %i, \"isFinal\": %i }",
                 name,             weight,           isPersistent,         chance,         isFinal
        );
    }
}

class HDCoreSpawnTableNestedEntry : HDCoreSpawnTableEntry {

    HDCoreSpawnTable _table;

    /**
     * Creates an entry for a given thing by name and its chances of spawning within the table.
     * @param {HDCoreSpawnTable} table The Spawn Table to use in this entry.
     * @param {double} [weight=-1.0] The relative weight/bias of the thing in the table. Defaults to the total weight of the table.
     * @param {bool} [persists=false] if true, the new entry is marked as persistent.
     * @param {int} [chance=256] chance out of 256 that the thing being spawned will actually spawn.
     * @param {bool} [isFinal=false] If true, the selected entry will be considered final.
     * @returns {HDCoreSpawnTableEntry} The newly created table entry.
     */
    static HDCoreSpawnTableEntry create(HDCoreSpawnTable table, double weight = -1.0, bool persists = false, int chance = 256, bool isFinal = false) {
        HDCoreSpawnTableNestedEntry entry = HDCoreSpawnTableNestedEntry(new('HDCoreSpawnTableNestedEntry'));

        entry._table = table;

        entry.name         = table.tableName;
        entry.weight       = weight;
        entry.isPersistent = persists;
        entry.chance       = chance;
        entry.isFinal      = isFinal;

        return entry;
    }

    override name getValue(bool persistentOnly) {
        let entry = _table.getRandomEntry(persistentOnly);

        // If a random value (0-255) falls below the success chance, return what's in the entry.
        // Otherwise return a "nothing" dummy spawner to simulate a RandomSpawner picking and failing to drop a thing.
        return (entry && (chance > 255 || random() < chance)) ? entry.getValue() : 'NullSpawner';
    }

    override double getWeight(bool persistentOnly) {
        if (weight >= 0.0) {
            return weight;
        } else {        
            Array<HDCoreSpawnTableEntry> entries;
            entries.clear();

            getEntries(entries, persistentOnly);

            return HDCoreSpawnTable.getTotalWeight(entries, persistentOnly);
        }
    }

    override void getEntries(out Array<HDCoreSpawnTableEntry> entries, bool persistentOnly) {
        _table.getEntries(entries, persistentOnly);
    }

    override string toString() {
        return String.format(
            "{ \"name\": \"%s\", \"table\": %s, \"weight\": %.3f, \"isPersistent\": %i, \"chance\": %i, \"isFinal\": %i }",
                 name,       _table.toString(),   getWeight(),      isPersistent,         chance,         isFinal
        );
    }
}
