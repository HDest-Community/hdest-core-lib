class HDCoreCommandParam abstract {

    string value;

    static HDCoreCommandParam create(name paramName, string value = "") {
        let param = HDCoreCommandParam(new(paramName));

        if (param) param.setParam(value);

        return param;
    }

    virtual void setParam(string newVal = "") {
        value = newVal;
    }

    abstract string getStringValue();

    abstract name getNameValue();

    abstract int getIntValue();

    abstract double getDoubleValue();

    abstract bool getBoolValue();
}

class HDCoreCommand {
    name command;

    Map<string, HDCoreCommandParam> params;

    static HDCoreCommand create(name command, string paramsStr = "") {
        let cmd = HDCoreCommand(new('HDCoreCommand'));

        if (cmd) {
            cmd.command = command;
            cmd.params.clear();

            if (paramsStr.length() > 0) {

                let dict = Dictionary.fromString(paramsStr);
                let i = DictionaryIterator.create(dict);

                while (i.next()) {
                    name paramName = '';
                    let key = i.key();
                    let val = i.value();

                    if (val.indexOf("$") == 0) {
                        paramName = 'HDCoreCommandLocalizedParam';
                        val = val.mid(1);
                    } else if (val.indexOf("#") == 0) {
                        paramName = 'HDCoreCommandCVarParam';
                        val = val.mid(1);
                    } else {
                        paramName = 'HDCoreCommandStaticParam';
                    }

                    let params = HDCoreCommandParam.create(paramName, val);

                    if (params) {
                        cmd.params.insert(key, params);
                    }
                }
            }
        }

        return cmd;
    }

    bool hasParam(string key) {
        return params.checkKey(key);
    }

    HDCoreCommandParam getParam(string key) {
        return HDCoreCommandParam(params.getIfExists(key));
    }

    string getStringParam(string key, string defaultValue = "") {
        return hasParam(key) ? getParam(key).getStringValue() : defaultValue;
    }

    name getNameParam(string key, name defaultValue = '') {
        return hasParam(key) ? getParam(key).getNameValue() : defaultValue;
    }

    int getIntParam(string key, int defaultValue = 0) {
        return hasParam(key) ? getParam(key).getIntValue() : defaultValue;
    }

    double getDoubleParam(string key, double defaultValue = 0.0) {
        return hasParam(key) ? getParam(key).getDoubleValue() : defaultValue;
    }

    bool getBoolParam(string key, bool defaultValue = false) {
        return hasParam(key) ? getParam(key).getBoolValue() : defaultValue;
    }

    bool equals(HDCoreCommand other) {

        // First check if the command and size of params are equal
        let isEqual = command == other.command && params.countUsed() == other.params.countUsed();

        // If they are, check if each param matches the other, and quit early if they are not
        if (isEqual) forEach(k, v : params) if (!other.params.checkKey(k) || other.params.getIfExists(k) != v) return false;

        return isEqual;
    }

    string toString() {
        let paramStr = "(";

        forEach (k, v : params) paramStr = paramStr.."\""..k.."\": "..HDCoreCommandParam(v).getStringValue()..", ";

        paramStr = paramStr..")";

        return String.format(
            "{ command=%s, params=%s }",
               command,    paramStr
        );
    }
}

