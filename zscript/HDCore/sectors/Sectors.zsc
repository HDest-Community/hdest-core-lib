extend class HDCore {
    
    /**
     * Returns details about the given sector's size.
     * @param {Sector} s The Sector to calculate
     * @returns {double} The area of the sector
     * @returns {double} The circumference of the sector
     * @returns {double} The radius of the sector
     * @returns {double} The average length of all linedefs in the sector
     */
    static clearscope double, double, double, double getSectorSize(Sector s) {
        let l = s.lines.size();

        // If the sector has no lines, return all 0s.
        if (!l) return 0.0, 0.0, 0.0, 0.0;

        // Otherwise calculate the size.
        let c = HDCore.getSectorCircumference(s);
        let r = HDCore.getSectorRadius(s);
        return HDCore.getSectorArea(s), c, r, c / l;
    }
    
    /**
     * Returns the given sector's area in map units.
     * @param {Sector} s The Sector to calculate
     * @returns {double} 
     */
    static clearscope double getSectorArea(Sector s) {
        let a = 0.0;

        forEach (l : s.lines) a += (l.v1.p.x * l.v2.p.y) - (l.v1.p.y * l.v2.p.x);

        return abs(a / 2.0);
    }
    
    /**
     * Returns the given sector's height in map units.
     * @param {Sector} s The Sector to calculate
     * @returns {double} 
     */
    static clearscope double getSectorHeight(Sector s) {
        return abs(s.centerCeiling() - s.centerFloor());
    }

    /**
     * Returns the given sector's volume in map units.
     * @param {Sector} s The Sector to calculate
     * @returns {double} 
     */
    static clearscope double getSectorVolume(Sector s) {
        return getSectorArea(s) * getSectorHeight(s);
    }

    /**
     * Returns the given sector's circumference.
     * @param {Sector} s The Sector to calculate
     * @returns {double} 
     */
    static clearscope double getSectorCircumference(Sector s) {
        let c = 0.0;

        forEach (l : s.lines) c += l.delta.length();

        return c;
    }

    /**
     * Returns the given sector's radius.
     * @param {Sector} s The Sector to calculate
     * @returns {double} 
     */
    static clearscope double getSectorRadius(Sector s) {
        let c = getSectorCircumference(s);

        return c / (2 * HDCONST_PI);
    }
    
    /**
     * Returns the given sector's center point in 3D space.
     * @param {Sector} s The Sector to calculate
     * @returns {double} 
     */
    static clearscope Vector3 getSectorCenter(Sector s) {
        return (s.centerspot.x, s.centerspot.y, HDGMGlobalMaths.LerpDouble(s.floorplane.ZAtPoint(s.centerspot), s.ceilingplane.ZAtPoint(s.centerspot), 0.5));
    }
}