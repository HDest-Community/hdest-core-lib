extend class HDCore {

    /**
     * Helper method to check if current level is still in "pre-spawn"
     *   wherein the number of tics has not crossed the configured threshold.
     */
    static clearscope bool isPreSpawn() {
        return Level.mapTime < hdc_prespawn_threshold;
    }

    /**
     * Helper method to spawn stuff
     *   with included logic for handling positions outside of the level,
     *   removing things spawned stuck, giving the spawned thing a bit of random velocity, etc.
     * @param {cls} cls The 'name' of the Class to spawn
     * @param {pos} pos The position to spawn the thing
     * @param {count} count The number of times to try and spawn the thing(s).  Defaults to 1.
     * @param {randomVel} randomVel When true, gives the thing spawned some random velocity.  Defaults to true.
     * @param {allowReplace} allowReplace When true, will tell ZDoom this actor can be replaced.  Defaults to true.
     * @param {allowInvalidPos} allowInvalidPos When false, will check if the spawned actor is stuck and remove if it is.  Defaults to false.
     * @returns {Actor} the most recently successfully spawned Actor, otherwise null.
     */
    static play Actor spawnStuff(name cls, vector3 pos, int count = 1, bool randomVel = true, bool allowReplace = true, bool allowInvalidPos = false) {
        
        // If we aren't spawning at least one thing, quit.
        if (count <= 0) return null;

        // If the thing trying to be spawned doesn't exist, quit.
        if (!HDCore.CheckClassExists(cls)) return null;
        
        // If the position to spawn isn't in the level, quit.
        if (!Level.IsPointInLevel(pos)) return null;
        
        Actor a;

        for (int i = 0; i < count; i++) {

            // FIXME: Clean up previously successful spawns if latest one fails?
            a = Actor.Spawn(cls, pos, allowReplace ? ALLOW_REPLACE : NO_REPLACE);

            // If the thing failed to spawn, quit.
            if (!a) return null;

            // If the thing spawned stuck, destroy it and quit.
            if (!(allowInvalidPos || a.TestMobjLocation())) {
                HDCore.log('HDCore.SpawnStuff', LOGGING_DEBUG, a.getClassName().." Spawned in invalid location "..pos..", Destroying.");

                a.destroy();

                return null;
            }

            if (randomVel) {
                a.vel += (
                    HDCore.getRandomDouble(-2, 2, hdc_random_mode),
                    HDCore.getRandomDouble(-2, 2, hdc_random_mode),
                    HDCore.getRandomDouble(-1, 2, hdc_random_mode)
                );
            }
        }

        return a;
    }
}