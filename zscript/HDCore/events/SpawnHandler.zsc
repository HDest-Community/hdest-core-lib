class HDCoreSpawnHandler : EventHandler {

    Array<name> mapBlacklist;

    // List of persistent classes to completely ignore.
    Array<name> thingBlacklist;

    // List of all spawn tables.
    Array<HDCoreSpawnTable> spawnTables;

    bool initialized;

    HDCoreSpawnTable getSpawnTable(name name, bool replace = true) {
        if (spawnTables.size()) {
            forEach (table : spawnTables) {
                if ((table.tableName == name || table.spawnName == name) && table.replace == replace) {
                    return table;
                }
            }
        }

        return null;
    }

    void getSpawnTables(out Array<HDCoreSpawnTable> tables, name name, bool replace = true) {
        if (spawnTables.size()) {
            forEach (table : spawnTables) {
                if ((table.tableName == name || table.spawnName == name) && table.replace == replace) {
                    tables.push(table);
                }
            }
        }
    }

    // Appends the given Spawn Table with the handler, or registers it if it does not yet exist
    void addSpawnTable(HDCoreSpawnTable table) {

        if (hd_debug) {

            let msg = "Adding "
                ..(table.replace ? "Replacement" : "Spawning")
                .." Table for "..table.spawnName.." named '"..table.tableName
                .."': [";

                if (table._entries.size()) forEach (entry : table._entries) msg = msg..", "..entry.toString();

            Console.printF(msg.."]");
        }

        // If we find an existing table, simply append the entries
        if (spawnTables.size()) {
            forEach (spawnTable : spawnTables) {
                if (table.equals(spawnTable)) {
                    spawnTable._entries.append(table._entries);

                    return;
                }
            }
        }

        // Otherwise, add the table to the registry.
        spawnTables.push(table);
    }

    void removeSpawnTable(name name, bool replace = true) {
        Array<HDCoreSpawnTable> tables;
        tables.clear();

        getSpawnTables(tables, name, replace);
        forEach (spawnTable : tables) spawnTables.delete(spawnTables.find(spawnTable));
    }

    // Add a new entry to an existing Spawn Table
    void addSpawnTableEntry(name name, HDCoreSpawnTableEntry entry, bool replace = true) {
        Array<HDCoreSpawnTable> tables;
        tables.clear();

        getSpawnTables(tables, name, replace);
        forEach (spawnTable : tables) {

            if (hd_debug) Console.printF("Adding "
                ..(entry.isPersistent ? "" : "non-").."persistent entry "
                .." for "..entry.name.." to the "
                ..(spawnTable.replace ? "Replacement" : "Spawning")
                .." Table named '"..spawnTable.tableName.."'"
            );

            spawnTable.addEntry(entry);
        }
    }

    // Tries to replace the thing during spawning.
    bool tryReplaceThing(ReplaceEvent e, HDCoreSpawnTable table) {

        let persistentOnly = Level.mapTime > 1;

        let entry = table.getRandomEntry(persistentOnly);

        if (entry) {
            let replacement = entry.getValue(persistentOnly);
    
            if (replacement != e.replacee && replacement != 'NullSpawner') {
                if (hd_debug) Console.printF(e.replacee.getClassName().." -> "..replacement);
    
                e.replacement = replacement;
                // TODO: Support marking replacements as final
                // e.final = entry.final || replacement == 'NullSpawner';
    
                return true;
            }
        }

        return false;
    }

    // Tries to create the thing via random spawning.
    bool tryCreateThing(Actor a, HDCoreSpawnTable table) {
        
        let persistentOnly = Level.mapTime > 1;

        let entry = table.getRandomEntry(persistentOnly);

        if (entry) {
            let value = entry.getValue(persistentOnly);

            if ((Class<Actor>)(value) && value != a.getClassName() && value != 'NullSpawner') {
                if (hd_debug) Console.printF(a.getClassName().." + "..value);
    
                Actor.Spawn(value, a.pos);
    
                return true;
            }
        }

        return false;
    }

    void init() {
        if (initialized) return;

        mapBlackList.clear();
        thingBlacklist.clear();
        spawnTables.clear();

        let cmdReader = HDCoreInfoReader(StaticEventHandler.find('HDCoreInfoReader'));

        loadSpawnTables(cmdReader.commands);

        if (hd_debug) {
            Console.printF("Configured Spawn Tables:");
            forEach(table : spawnTables) Console.printF(" * "..table.toString());
        }

        initialized = true;
    }

    void loadSpawnTables(Array<HDCoreCommand> cmds) {
        forEach (cmd : cmds) {
            switch (cmd.command) {
                case 'addSpawnHandlerThingBlacklist': {
                    let name = cmd.getNameParam("name");

                    if (name) thingBlacklist.push(name);
                    break;
                }
                case 'addSpawnHandlerMapBlacklist': {
                    let name = cmd.getNameParam("name");

                    if (name) mapBlacklist.push(name);
                    break;
                }
                case 'newSpawnTable': {
                    let spawnName   = cmd.getNameParam("spawnName");
                    let tableName   = cmd.getNameParam("tableName");
                    let replacement = cmd.getBoolParam("replaces", true);

                    // If one already exists, quit.
                    if (getSpawnTable(tableName ? tableName : spawnName, replacement)) break;

                    // Create the new
                    let table = HDCoreSpawnTable.create(spawnName, tableName, replacement);

                    if (table) {
                        if (cmd.getBoolParam("addOriginalThing")) {
                            let entry = HDCoreSpawnTableSingleEntry.create(
                                spawnName,
                                cmd.getDoubleParam("weight", 1.0),
                                cmd.getBoolParam("persists"),
                                cmd.getIntParam("chance", 256)
                            );
    
                            if (entry) table.addEntry(entry);
                        }

                        addSpawnTable(table);
                    }
                    break;
                }
                case 'addSpawnTableSingleEntry': {
                    let entry = HDCoreSpawnTableSingleEntry.create(
                        cmd.getNameParam("name"),
                        cmd.getDoubleParam("weight", 1.0),
                        cmd.getBoolParam("persists"),
                        cmd.getIntParam("chance", 256)
                    );

                    if (entry) addSpawnTableEntry(
                        cmd.getNameParam("tableName"),
                        entry,
                        cmd.getBoolParam("replaces", true)
                    );
                    break;
                }
                case 'addSpawnTableNestedEntry': {
                    let nestedTable = getSpawnTable(
                        cmd.getNameParam("name"),
                        cmd.getBoolParam("replaces", true)
                    );

                    if (!nestedTable) {
                        nestedTable = HDCoreSpawnTable.create(
                            cmd.getNameParam("name"),
                            cmd.getNameParam("name"),
                            cmd.getBoolParam("replaces", true)
                        );

                        if (nestedTable) addSpawnTable(nestedTable);
                    }

                    let entry = HDCoreSpawnTableNestedEntry.create(
                        nestedTable,
                        cmd.getDoubleParam("weight", 1.0),
                        cmd.getBoolParam("persists"),
                        cmd.getIntParam("chance", 256)
                    );
    
                    if (entry) addSpawnTableEntry(
                        cmd.getNameParam("tableName"),
                        entry,
                        cmd.getBoolParam("replaces", true)
                    );
                    break;
                }
                case 'removeSpawnTableEntry': {
                    let table = getSpawnTable(
                        cmd.getNameParam("tableName"),
                        cmd.getBoolParam("replaces", true)
                    );

                    if (table) table.removeEntry(cmd.getNameParam("name"));
                    break;
                }
                case 'clearSpawnTable': {
                    let table = getSpawnTable(
                        cmd.getNameParam("tableName"),
                        cmd.getBoolParam("replaces", true)
                    );

                    if (table) table.clearEntries();
                    break;
                }
                case 'appendSpawnTableEntries': {
                    let sourceTable = getSpawnTable(
                        cmd.getNameParam("sourceTableName"),
                        cmd.getBoolParam("replaces", true)
                    );
                    
                    let destTable = getSpawnTable(
                        cmd.getNameParam("destTableName"),
                        cmd.getBoolParam("replaces", true)
                    );

                    if (sourceTable && destTable) destTable.addEntries(sourceTable._entries);
                    break;
                }
                case 'setSpawnTableEntryWeight': {
                    let table = getSpawnTable(
                        cmd.getNameParam("tableName"),
                        cmd.getBoolParam("replaces", true)
                    );

                    if (table) {
                        let entry = table.getEntry(cmd.getNameParam("name"));
                        let weight = cmd.getDoubleParam("weight", 1.0);

                        if (entry) entry.weight = weight > 0 ? weight : entry.getWeight();
                    }
                    break;
                }
                case 'removeSpawnTable': {
                    removeSpawnTable(
                        cmd.getNameParam("name"),
                        cmd.getBoolParam("replaces", true)
                    );
                    break;
                }
                case 'removeAllSpawnTables': {
                    spawnTables.clear();
                    break;
                }
                default:
                    break;
            }
        }
    }

    override void worldLoaded(WorldEvent e) {

        // Populates the main arrays if they haven't been already.
        if (!initialized) init();
    }

    override void checkReplacement(ReplaceEvent e) {

        // Populates the main arrays if they haven't been already.
        if (!initialized) init();

        // If there's nothing to replace or if the replacement is final, quit.
        if (!e.replacee || e.isFinal) return;

        // If thing being replaced is blacklisted, quit.
        forEach (bl : thingBlacklist) if (e.replacee is bl) return;

        // If current map is blacklisted, quit.
        forEach (bl : mapBlackList) if (Level.MapName == bl) return;

        handleSpawnReplacements(e);
    }

    override void worldThingSpawned(WorldEvent e) {

        // Populates the main arrays if they haven't been already.
        if (!initialized) init();

        // If thing spawned doesn't exist, quit.
        if (!e.thing) return;

        // If thing spawned is blacklisted, quit.
        forEach (bl : thingBlacklist) if (e.thing is bl) return;

        // If current map is blacklisted, quit.
        forEach (bl : mapBlackList) if (Level.MapName == bl) return;

        handleSpawns(e.thing);
    }

    private void handleSpawnReplacements(ReplaceEvent e) {

        // Iterate through the list of replacement tables for the current thing
        Array<HDCoreSpawnTable> tables;
        tables.clear();

        getSpawnTables(tables, e.replacee.getClassName(), true);
        forEach (table : tables) {
            if (hd_debug) Console.printF("Attempting to replace "..e.replacee.getClassName().." with entry in "..table.tableName.."...");

            // If an entry in the table is selected and is replaced successfully, quit.
            if (tryReplaceThing(e, table)) return;
        }
    }

    private void handleSpawns(Actor thing) {

        // Iterate through the list of non-replacement tables for the current thing
        Array<HDCoreSpawnTable> tables;
        tables.clear();

        getSpawnTables(tables, thing.getClassName(), false);
        forEach (table : tables) {

            // if currently in pre-spawn or configured to be persistent,
            // and original thing being spawned is not an owned item,
            // and not configured to replace original spawn,
            // attempt to spawn new thing.
            let item = Inventory(thing);
            if (!(item && item.owner)) {
                if (hd_debug) Console.printF("Attempting to spawn entry in "..table.tableName.." along with "..thing.getClassName().."...");

                // If an entry in the table is selected and is spawned successfully, quit.
                if (tryCreateThing(thing, table)) return;
            }
        }
    }
}
