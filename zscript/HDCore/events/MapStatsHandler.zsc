class HDCoreMapStatsHandler : HDCoreEventHandler {

    Dictionary _stats;

    Array<name> blacklist;

    override void beforeProcessCommands() {
        _stats = Dictionary.create();

        blacklist.clear();
    }
    
    override void afterProcessCommands() {
        blacklist.copy(HDCoreSpawnHandler(StaticEventHandler.find('HDCoreSpawnHandler')).thingBlacklist);
    }


    // ----------
    // PUBLIC API
    // ----------

    clearscope string getValue(name key) {
        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Getting Map Stat '"..key.."'...");

        let ret = _stats ? _stats.at(key) : "";

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Value: "..ret);

        return ret;
    }

    clearscope string getValueOrDefault(name key, string defaultValue = "") {
        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Getting Map Stat '"..key.."', or '"..defaultValue.."' if it doesn't...");

        let val = getValue(key);
        let ret = val ? val : defaultValue;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Value: "..ret);

        return ret;
    }

    clearscope void setValue(name key, string value) {
        _stats.insert(key, value);
    }


    // ------
    // EVENTS
    // ------

    override void OnRegister() {
        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Handler Registered...");

        super.OnRegister();

        SetOrder(-100);

        init();
    }

    override void WorldLoaded(WorldEvent e) {
        init();

        // If Stat Tracking is disabled, quit.
        if (!hdc_stat_trackers) return;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "World Loaded...");

        incrementLevelsStarted();
    }

    override void WorldThingSpawned(WorldEvent e) {
        init();

        // If Stat Tracking is disabled, quit.
        if (!hdc_stat_trackers) return;

        // If thing is invalid, quit.
        if (!e.thing || !(e.thing is 'Actor') || ((e.thing is 'Inventory') && Inventory(e.thing).owner)) return;

        // If thing is blacklisted, quit.
        // TODO: Track separately from SpawnHandler's blacklist?
        forEach (bl : blacklist) if (e.thing is bl) return;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Thing Spawned...");

        incrementThingsSpawned(e.thing.getClassName());
    }

    override void WorldThingDied(WorldEvent e) {
        init();

        // If Stat Tracking is disabled, quit.
        if (!hdc_stat_trackers) return;

        // If thing is invalid, quit.
        if (!e.thing || !(e.thing is 'Actor') || ((e.thing is 'Inventory') && Inventory(e.thing).owner)) return;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Thing Died...");

        incrementThingsDied(e.thing.getClassName());
    }

    override void WorldUnloaded(WorldEvent e) {
        // If Stat Tracking is disabled, quit.
        if (!hdc_stat_trackers) return;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "World Unloaded...");

        incrementLevelsCompleted();
    }


    // ------------
    // INTERNAL API
    // ------------

    protected void incrementLevelsStarted() {
        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Incrementing Levels Started MapStat...");

        int prevValue = getValueOrDefault('levels.started', "0").toInt(10);
        int currValue = prevValue + 1;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Levels Started: "..prevValue.." -> "..currValue);

        setValue('levels.started', currValue.."");
    }

    protected void incrementThingsSpawned(name name) {
        int prevValue = getValueOrDefault(name..'.spawned', "0").toInt(10);
        int currValue = prevValue + 1;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Number of "..name.." Spawned: "..prevValue.." -> "..currValue);

        setValue(name..'.spawned', currValue.."");
    }

    protected void incrementThingsDied(name name) {
        int prevValue = getValueOrDefault(name..'.died', "0").toInt(10);
        int currValue = prevValue + 1;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Number of "..name.." Died: "..prevValue.." -> "..currValue);

        setValue(name..'.died', currValue.."");
    }

    protected void incrementLevelsCompleted() {
        int prevValue = getValueOrDefault('levels.completed', "0").toInt(10);
        int currValue = prevValue + 1;

        HDCore.log('HDCoreLib.MapStatsHandler', LOGGING_TRACE, "Levels Completed: "..prevValue.." -> "..currValue);

        setValue('levels.completed', currValue.."");
    }
}