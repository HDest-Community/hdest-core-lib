class HDCoreAmmoUsesHandler : HDCoreEventHandler {

    // List of persistent classes to completely ignore.
    Array<name> thingBlacklist;

    // List of weapon-ammo associations.
    // Used for ammo-use association on ammo spawn (happens very often).
    Array<HDCoreSpawnAmmo> ammoSpawnList;

    override void beforeProcessCommands() {
        thingBlacklist.clear();
        ammoSpawnList.clear();
    }

    override void processCommand(HDCoreCommand cmd) {
        switch (cmd.command) {
            case 'addAmmoThingBlacklist': {
                let name = cmd.getNameParam("name");

                if (name) thingBlacklist.push(name);
                break;
            }
            case 'newAmmo': {
                let ammo = HDCoreSpawnAmmo.create(cmd.getNameParam("ammoName"));

                if (ammo) addAmmo(ammo);
                break;
            }
            case 'addAmmoUser': {
                addAmmoUser(cmd.getNameParam("ammoName"), cmd.getNameParam("name"));

                break;
            }
            case 'removeAmmo': {
                removeAmmo(cmd.getNameParam("ammoName"));

                break;
            }
            case 'removeAllAmmo': {
                ammoSpawnList.clear();
                break;
            }
            default:
                break;
        }
    }

    override void afterProcessCommands() {
        if (HDCore.ShouldLog('HDCoreLib', LOGGING_DEBUG)) {

            let msg = "Configured Ammo Usage List:\n";

            forEach(ammo : ammoSpawnList) msg = msg..ammo.toString().."\n";
            
            HDCore.Log('HDCoreLib', LOGGING_DEBUG, msg);
        }
    }

    HDCoreSpawnAmmo getAmmo(name ammoName) {
        HDCoreSpawnAmmo ammoSpawn;

        if (ammoSpawnList.size()) {
            forEach (ammo : ammoSpawnList) if (ammo.ammoName == ammoName) return ammo;
        }

        return null;
    }

    // appends an entry to ammoSpawnList;
    void addAmmo(HDCoreSpawnAmmo ammo) {

        if (HDCore.ShouldLog('HDCoreLib', LOGGING_DEBUG)) {

            let msg = "Adding Ammo Association Entry for "..ammo.ammoName..": [";

            if (ammo.userNames.size()) forEach (userName : ammo.userNames) msg = msg..", "..userName;

            HDCore.Log('HDCoreLib', LOGGING_DEBUG, msg.."]");
        }

        let existingAmmo = getAmmo(ammo.ammoName);

        // If we find an existing Ammo Association, simply merge the two lists
        if (existingAmmo) {
            existingAmmo.userNames.append(ammo.userNames);

            return;
        }

        // Otherwise, add the association to the registry
        ammoSpawnList.push(ammo);
    }

    void removeAmmo(name ammoName) {
        let ammo = getAmmo(ammoName);

        if (ammo) ammoSpawnList.delete(ammoSpawnList.find(ammo));
    }

    // appends an entry to ammoSpawnList;
    void addAmmoUser(name ammoName, name userName) {

        HDCore.Log('HDCoreLib', LOGGING_DEBUG, "Adding \""..userName.."\" to Ammo Association Entry for "..ammoName);
        
        let existingAmmo = getAmmo(ammoName);

        // If we find an existing Ammo Association, simply add the new entry
        if (existingAmmo) {

            existingAmmo.userNames.push(userName);

            return;
        }

        // OTherwise, create the association and add it to the registry
        let ammo = HDCoreSpawnAmmo.create(ammoName);
        ammo.userNames.push(userName);

        ammoSpawnList.push(ammo);
    }

    override void worldThingSpawned(WorldEvent e) {

        // Populates the main arrays if they haven't been already.
        if (!initialized) init();

        // If there are no configured ammo spawns, quit.
        if (!ammoSpawnList.size()) return;

        // If thing spawned doesn't exist, quit.
        if (!e.thing) return;

        // If thing spawned is blacklisted, quit.
        foreach (bl : thingBlacklist) if (e.thing is bl) return;

        // Pointers for specific classes.
        let ammo = HDAmmo(e.thing);

        // If the thing spawned is an ammunition, add any and all items that can use this.
        if (ammo) handleAmmoUses(ammo);
    }

    private void handleAmmoUses(HDAmmo ammo) {
        foreach (ammoSpawn : ammoSpawnList) {
            if (ammo.getClassName() == ammoSpawn.ammoName && ammoSpawn.userNames.size() > 0) {

                if (HDCore.ShouldLog('HDCoreLib', LOGGING_DEBUG)) {

                    let msg = "Adding the following to the list of items that use "..ammo.getClassName().."\n";
    
                    foreach (weapon : ammoSpawn.userNames) msg = msg.." * "..weapon.."\n";
                    
                    HDCore.Log('HDCoreLib', LOGGING_DEBUG, msg);
                }

                forEach(userName : ammoSpawn.userNames) ammo.itemsThatUseThis.push(userName);
            }
        }
    }
}
