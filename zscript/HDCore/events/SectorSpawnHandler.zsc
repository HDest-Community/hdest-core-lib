class HDCoreSectorSpawnData {
    name spawnName;
    Vector3 pos;

    static HDCoreSectorSpawnData create(name spawnName, Vector3 pos) {
        let data = HDCoreSectorSpawnData(new('HDCoreSectorSpawnData'));

        if (data) {
            data.spawnName = spawnName;
            data.pos = pos;
        }

        return data;
    }
}

class HDCoreSectorSpawnerHandler : EventHandler {

    Array<HDCoreSectorSpawnData> spawnPositions;
    
    override void OnRegister() {
        // Spawn Dummy Actors after everything else
        SetOrder(666);
    }

    override void WorldLoaded(WorldEvent e) {

        // TODO: Enhance Check for Sector-based Spawners being enabled/disabled
        //   Actor.GetReplacement() static method to pre-emptively replace, or skip if not replaced?
        //   Service for addons to inform HDCoreLib they want to leverage the feature, with user-specific override if desired?

        // If Sector-based Spawners are explicitly disabled, quit.
        if (!hdc_sector_spawners < 0) return;

        // If we're re-opening the level, quit.
        if (e.IsReopen) return;

        // Collect all of the positions to spawn in the level.
        BuildSpawnPositions();
    }

    override void WorldTick() {

        if (HDCore.isPreSpawn() && hdc_sector_spawners >= 0) {
            HandleSectorSpawning();
        } else if (!bDESTROYED) {
            destroy();
        }
    }

    private void BuildSpawnPositions() {
        spawnPositions.clear();

        let minX = Int.MAX;
        let minY = Int.MAX;
        let maxX = Int.MIN;
        let maxY = Int.MIN;

        forEach(s : Level.sectors) forEach (l : s.lines) {
            if (l.v1.p.x < minX) minX = l.v1.p.x;
            if (l.v1.p.x > maxX) maxX = l.v1.p.x;

            if (l.v1.p.y < minY) minY = l.v1.p.y;
            if (l.v1.p.y > maxY) maxY = l.v1.p.y;

            if (l.v2.p.x < minX) minX = l.v2.p.x;
            if (l.v2.p.x > maxX) maxX = l.v2.p.x;

            if (l.v2.p.y < minY) minY = l.v2.p.y;
            if (l.v2.p.y > maxY) maxY = l.v2.p.y;
        }

        HDCore.Log('HDCoreLib.SectorSpawnHandler', LOGGING_DEBUG, "Sampling map area from "..(minX, minY).." to "..(maxX, maxY).."...");
        
        let incr = HDCONST_ONEMETRE * hdc_sector_spawner_sampleRate;
        for (let x = minX; x < maxX; x+= incr) for (let y = minY; y < maxY; y += incr) {
            let xy = (x, y);
            
            let s = Level.PointInSector(xy);
            if (s) {

                // If the sector has 0 height or the floor is the skyflat, skip.
                if (!HDCore.getSectorHeight(s) || s.GetTexture(0) == skyflatnum) continue;

                let pos = (x, y, s.centerFloor());

                // Determine dummy spawner(s), add to list.
                if (s.IsSecret()) addSpawnPos('SecretSectorSpawner', pos);

                if (s.GetTexture(1) == skyflatnum) addSpawnPos('OutdoorSectorSpawner', pos); 
                else                               addSpawnPos('IndoorSectorSpawner', pos);
                
                if (s.damageAmount > 0) addSpawnPos('HurtfloorSectorSpawner', pos);

                // Divide all 65536 units of light level into 7 evenly spaced "regions" for spawning purposes
                //      if (s.lightLevel > 56174) brightestSectors.Push(s); // 56175..65536 =  9362 units
                // else if (s.lightLevel > 46812) brighterSectors.Push(s);  // 46813..56174 =  9362 units
                // else if (s.lightLevel > 37450) brightSectors.Push(s);    // 37451..46812 =  9362 units
                // else if (s.lightLevel > 28086) {}                        // 28087..37450 =  9364 units
                // else if (s.lightLevel > 18724) darkSectors.Push(s);      // 18725..28086 =  9362 units
                // else if (s.lightLevel >  9362) darkerSectors.Push(s);    //  9363..18724 =  9362 units
                // else if (s.lightLevel >     0) darkestSectors.Push(s);   //     0..9362  =  9362 units
                //                                                          //              = 65536 units
                
                // TODO: Enhance to detect if/when levels use the remaining 65280 units of light level
                // Divide all 256 units of light level into 7 evenly spaced "regions" for spawning purposes
                     if (s.lightLevel > 220) addSpawnPos('BrightestSectorSpawner', pos); // 221..256 =  36 units
                else if (s.lightLevel > 184) addSpawnPos('BrighterSectorSpawner', pos);  // 185..220 =  36 units
                else if (s.lightLevel > 148) addSpawnPos('BrightSectorSpawner', pos);    // 149..184 =  36 units
                else if (s.lightLevel > 108) {}                                          // 109..148 =  40 units
                else if (s.lightLevel >  72) addSpawnPos('DarkSectorSpawner', pos);      //  73..108 =  36 units
                else if (s.lightLevel >  36) addSpawnPos('DarkerSectorSpawner', pos);    //  37..72  =  36 units
                else if (s.lightLevel >   0) addSpawnPos('DarkestSectorSpawner', pos);   //   0..35  =  36 units
                                                                                         //          = 256 units
            }
        }

        HDCore.Log('HDCoreLib.SectorSpawnHandler', LOGGING_DEBUG, "Sampled "..spawnPositions.size().."x Spawn Positions");
    }

    private void addSpawnPos(name spawnName, Vector3 pos) {
        spawnPositions.push(HDCoreSectorSpawnData.create(spawnName, pos));
    }

    private void HandleSectorSpawning() {
        let max = spawnPositions.size();
        let incr = max(hdc_prespawn_threshold, 1);
        for (let i = Level.mapTime; i < max; i += incr) SpawnDummySpawners(spawnPositions[i]);
    }

    private void SpawnDummySpawners(HDCoreSectorSpawnData data) {

        Class<Actor> spawnerCls = data.spawnName;
        Class<Actor> replacement = Actor.getReplacement(data.spawnName);

        // If Sector-based Spawners are conditionally enabled,
        // and we didn't find a replacement actor for the dummy spawner, quit.
        if (!hdc_sector_spawners && (spawnerCls == replacement || replacement == 'NullSpawner')) return;

        // Get the default properties of the thing to spawn
        let replacementDefaults = getDefaultByType(replacement);

        // If the sector isn't tall enough, quit.
        if (!HDCore.getSectorHeight(Level.pointInSector(data.pos.xy)) < replacementDefaults.height) return;

        // If the thing failed to spawn, quit.
        if (!HDCore.SpawnStuff(replacement.getClassName(), data.pos)) return;

        HDCore.Log('HDCoreLib.SectorSpawnHandler', LOGGING_DEBUG, "Spawned '"..replacement.getClassName().."' @ "..data.pos);
    }
}
